
<p>Source is the main command of this query language, as it is the one that will bring the data into your query.
It lets you choose one of the registered sources and that will be used to fetch data.</p>

<p>The first parameter of the source command is the source name. 
You can hit Ctrl+Space after typing source to get a list of available sources.
Here a small example of using the command with the source 'random':</p>

<pre><code>source random</code></pre>


<h2>Source Parameters</h2>
<p>A source can have additional parameters, which are given to the command as key=value pairs. 
The 'random' source provides the optional parameter 'records' to specify how many random records should be generated.
</p>
<pre><code class="language-cfwquery">source random records=800</code></pre>



<h2>Fetch Limit</h2>
<p>The source command itself also has its own parameter called 'limit'. This parameter defined the maximum records that are fetched from the source.
The default and maximum values for this parameter are set by your system administrator to reduce the performance impact of the queries.
</p>
<p>For example, if you want to generate 100'000 records, but the default fetch limit is 10'000, you can specify the limit
</p>

<pre><code class="language-cfwquery">source random records=100000 limit=100000</code></pre>


<h2>Chain Multiple Sources</h2>
<p>To combine data from multiple sources, you can use this command multiple times in the same query.
The sources are read in order. First all data from the first source is read, then all data from the second and so forth. </p>

<p>The following example combines multiple sources in the same query. </p>

<pre><code class="language-cfwquery">| source random records=100
| top 4
| source random records=2
| source json data='
[{"ID": "test", "FIRSTNAME": "Serena", "LASTNAME": "Aloha"},
 ,{"ID": "test", "FIRSTNAME": "Alena", "LASTNAME": "Konnichiwa"},
 ,{"ID": "test", "FIRSTNAME": "Kassandra", "LASTNAME": "Ciaobella"}
]' 
| source random records=42</code></pre>


<p>Be aware that other commands still work the same. They will process the records in the order they are passed through the whole command chain. 
The following top command will take all 10 records from the first source, and the first 2 from the second source:</p>

<pre><code class="language-cfwquery">| source random records=10
| source random records=20
| top 12
</code></pre>




<h2>Fetch For Each</h2>
<p>With the each parameter, you can define a list of values, for which you want to execute the same source.
The values can be retrieved by using the sourceEach()-function. </p>

<p>The following example executes the source "text" and creates a hello message to greet the beautiful ladies:</p>
<pre><code class="language-cfwquery">| source text 
	each = ["Freya"
		,"Hera"
		,"Eclypsia"
	]
	text="Hello " + sourceEach()</code></pre>


<p>The following example executes the source "web" for each of the defined URLs:</p>
<pre><code class="language-cfwquery">| source web 
	as = "http"
	method = "GET"
	each = [
		 "https://www.xresch.com"
		,"https://github.com/xresch"
		,"https://www.google.com" 
	]
	url = sourceEach() # use the sourceEach()-function to get the value from the source
| keep status, url, headers
| formatfield headers = list
| formatrecord 	
    [(status == null), "cfw-gray"] 
	[(status == 200), "cfw-green"] 
	[(status &lt; 300), "cfw-limegreen"] 
	[(status &lt; 400), "cfw-yellow"] 
	[true, "cfw-red"] 
| display as=panels menu=false titlefields=[url]</code></pre>


<h2>Pagination</h2>
<p>Some sources like 'web' might require the use of pagination to be able to fetch all the data from a source.
Typical example would be a web API that limits the amount of data returned. </p>

<p>The following example sets the various pagination parameters and uses the page-value acquired with the sourcepage()-function to execute the random source several times:</p>
<pre><code class="language-cfwquery">| source random 
	records = 3 					# for demonstration, make 3 random records per page
	pagination = true 				# enable pagination
	pageinitial = 1 				# initial page value, retrievable with sourcepage() 
	page = sourcepage() + 1 		# adjust the initial value after every page
	pageend = ( sourcepage() > 5 )  # checked after page values has been adjusted, return true when last page has been reached
	pagemax = 20 					# the max number of pages to fetch, prevents endless loops</code></pre>


<p><b>Each:&nbsp;</b> Pagination can be combined with the each-parameter, in this case pagination is done for every each value:</p>
<pre><code class="language-cfwquery">| source random 
	each = [10, 10, 10] # every each-value will be fetched in a paginated manner 
	records = sourceEach() # get the each-value
	pagination = true 
	pageinitial = 1 
	page = sourcepage() + 1 
	pageend = ( sourcepage() >= 5 ) 
	pagemax = 20 </code></pre>

<p><b>Using Time Steps:&nbsp;</b> You can use pagination to step through a timeframe. 
In this case you won't use the sourcepage()-function to retrieve the page, 
but you will use earliest() / latest() for fetching parts of the timeframe.
This approach can be useful if you have for example a Web API that highly limits the number of records it returns per call, 
but allows you to filter the data by time. One disadvantage of this is that you have to choose a timeframe which does not reach the APIs data limit.
Following is an example on how to setup a pagination with time steps:</p>
<pre><code class="language-cfwquery">| globals
	"INITIAL_LATEST" = latest() # store the initial latest()
	STEP = 60  # amount to shift the timeframe for each page
	UNIT = 'm' # unit for STEP
| source json 
	# this is what the source will create
	data = object(
			  "INDEX", count() 
			, "INITIAL_LATEST", g("INITIAL_LATEST")			 # will stay the same
			, "earliest", earliest() 						 # use earliest() and latest() to get the time
			, "latest", min([latest(), g("INITIAL_LATEST")]) # makes sure to not overstep latest while still fetching full timeframe
		 )
	pagination 	= true	
	
	pageinitial	= latestSet(earliest(), g(STEP), g(UNIT)) 	# set latest to earliest() +
	
	page 		= timeframeOffset(0, g(STEP), g(UNIT))		# each page, shift the timeframe to the future by PAGE_STEP
	
	pageend		= earliest() >= g("INITIAL_LATEST")	# check if earliest is outside the timeframe
				   
	pagemax = 100 # will define the max timeframe: (pagemax * STEP) - for Example: 
	
| formatfield
	[earliest, latest, INITIAL_LATEST] = timestamp</code></pre>



