
<p><b>Video Tutorial:&nbsp;</b> <a target="newtab" href="https://www.youtube.com/watch?v=2ronjgzje2Y"><i class="fab fa-youtube"></i>&nbsp;Youtube</a> </p>

<p>Creates a new field and formats the specified values as a boxplot.</p>


<h3>Minimal Example</h3>
<p>Following is a minimal example on how to create a box plot. In this case we only get a simple box and the result ends up looking like a gantt chart: </p>
<pre><code class="language-cfwquery">| source random type=batchjobs seriesCount=2 records=10
| remove RESULTS, ERRORS 
| formatboxplot
	min = "START"
	max = "END"
</code></pre>


<h3>Formatting Timestamps</h3>
<p>In the previous examples we use times of batch jobs. To get epoch time formatted as timestamps in the popovers, set epoch = true: </p>
<pre><code class="language-cfwquery">| source random type=batchjobs seriesCount=5 records=10
| remove RESULTS, ERRORS 
| formatboxplot
	min = "START"
	max = "END"
	epoch = true
| display menu=false</code></pre>


<h3>Coloring By Groups</h3>
<p>To differentiate between different groups, you can set a color. Plus you can set a width so you can see the boxes better.
Let's also add QUEUED and REPORTED to let our boxes be real boxplots:</p>
<pre><code class="language-cfwquery">| source random type=batchjobs seriesCount=5 records=10
| formatboxplot
	min = "QUEUED"
	low = "START"
	high= "END"
	max = "REPORTED"
	epoch = true
	color = randomColorGroup(SERVICE, 50, 100, 40, 80)
	width = "400px"
| display menu=false
| remove SERVICE, RESULTS, ERRORS 
| formatfield
	[QUEUED, START, END, REPORTED] = timestamp
	DURATION = duration</code></pre>


<h3>Working with OHCL Data</h3>
<p> Be aware when using OHLC (Open, High, Low, Close) chart data, that the parameters have to be assigned like this: </p>
<pre><code class="language-cfwquery">| globals
	BY = ["NAME"]
| source random type=trading seriesCount = 3 records = 10
| formatboxplot
	by = g(BY)
	relative = true
	min = "LOW"
	low = "OPEN"
	high = "CLOSE"
	max = "HIGH"
	color = randomColorGroup("NAME", 50, 100, 40, 80)
| off formatfield
	Boxes = none
| sort g(BY)</code></pre>


<h3>Typical Statistics</h3>
<p>Following example creates typical statistics for boxplot charts and color them based on the median value: </p>
<pre><code class="language-cfwquery">| source random type=series seriesCount = 30 records = 100
| stats
	by = ["WAREHOUSE", ITEM]
	"MIN" = min(COUNT)
	"P25" = perc(COUNT, 25)
	"P50" = perc(COUNT, 50)
	"P75" = perc(COUNT, 75)
	"MAX" = max(COUNT)
| sort MAX reverse = true
| formatboxplot
	field 	 = "Boxes"
	relative = true
	min 	 = "MIN"
	low 	 = "P25"
	median 	 = "P50"
	high 	 = "P75"
	max 	 = "MAX"
	width 	 = "100%"
	height	 = "20px"
	color 	 = case(
		 ("P50" >= 100), "red" 
		,("P50" >= 90), "orange" 
		,("P50" >= 70), "yellow" 
		,("P50" >= 50), "limegreen" 
		, true, "green" 
	)

</code></pre>

<h3>Percentage Bars</h3>
<p>If you want to get creative, following example shows how you can get colored percentage bars: </p>
<pre><code class="language-cfwquery">
| source random records=30
| keep FIRSTNAME, LASTNAME, VALUE
| set VALUE = if(random() > 50, VALUE, VALUE * -1)
| formatboxplot 
	min = if(VALUE &lt;= 0, VALUE, 0 )
	max = if(VALUE >= 0, VALUE, 0 )
	width = "40vw"
	relative = true
	color = case(
		 (VALUE &lt; 0), "red" 
		, (VALUE > 0), "green" 
		, true, "green" 
	)
</code></pre>










