
<p>Creates a new field and formats the specified values as a boxplot.</p>


<p>Following is a minimal example on how to create a box plot. In this case we only get a simple box and the result ends up looking like a gantt chart: </p>
<pre><code class="language-cfwquery">| source random type=batchjobs seriesCount=2 records=10
| remove RESULTS, ERRORS 
| formatboxplot
	min = "START"
	max = "END"
</code></pre>



<p>To differentiate between different groups, you can set a color. Plus you can set a width so you can see the boxes better: </p>
<pre><code class="language-cfwquery">| source random type=batchjobs seriesCount=5 records=10
| remove RESULTS, ERRORS 
| formatboxplot
	min = "START"
	max = "END"
	color = randomColorGroup(SERVICE)
	width = "500px"
| display defaultsize=500</code></pre>



<p>You can also create box plots and color them by a specified group: </p>
<pre><code class="language-cfwquery">| globals
	BY = ["NAME"]
| source random type=trading seriesCount = 3 records = 10
| formatboxplot
	by = g(BY)
	relative = true
	min = "LOW"
	low = "OPEN"
	high = "CLOSE"
	max = "HIGH"
	color = randomColorGroup("NAME", 50, 100, 40, 80)
| off formatfield
	Boxes = none
| sort g(BY)</code></pre>



<p>Following example creates typical statistics for boxplot charts and color them based on the median value: </p>
<pre><code class="language-cfwquery">| source random type=series seriesCount = 30 records = 100
| stats
	by = ["WAREHOUSE", ITEM]
	"MIN" = min(COUNT)
	"P25" = perc(COUNT, 25)
	"P50" = perc(COUNT, 50)
	"P75" = perc(COUNT, 75)
	"MAX" = max(COUNT)
| sort MAX reverse = true
| formatboxplot
	field 	 = "Boxes"
	relative = true
	min 	 = "MIN"
	low 	 = "P25"
	median 	 = "P50"
	high 	 = "P75"
	max 	 = "MAX"
	width 	 = "100%"
	height	 = "20px"
	color 	 = case(
		 ("P50" >= 100), "red" 
		,("P50" >= 90), "orange" 
		,("P50" >= 70), "yellow" 
		,("P50" >= 50), "limegreen" 
		, true, "green" 
	)

</code></pre>








