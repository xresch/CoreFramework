
<p><b>Video Tutorial:&nbsp;</b> <a target="newtab" href="https://www.youtube.com/watch?v=mzTP5OpVWMM&pp=0gcJCa0JAYcqIYzv"><i class="fab fa-youtube"></i>&nbsp;Youtube</a> </p>

<p>This command lets you render a chart from your data.</p>

<p>The following query creates random series data and displays them as a line chart: </p>
<pre><code class="language-cfwquery">| source random records=100 type=series
| chart 
	by=[WAREHOUSE, ITEM]
	x=TIME 
	y=COUNT </code></pre>
	
<p>You can provide multiple columns for the y-axis by using an array. Also you can leave the by-parameter away for no grouping of series: </p>
<pre><code class="language-cfwquery">| source random records=100 type="stats" seriesCount = 1
| chart 
	type = line
	x = TIME 
	y = [MIN, AVG, P90, MAX]
	tension = 0.4 # 0 to 1, makes lines smoother
	pointradius=2</code></pre>
	
	
	
<h2>Display Options</h2>
<p>You can change the look and feel of your chart using various options. </p>
<pre><code class="language-cfwquery">| source random records=100 type=series
| chart 
	by = [WAREHOUSE, ITEM] 
	type = area
	x = TIME 
	y = COUNT
	height = '250px'
	stacked = true
	showaxes = true
	showlegend = false 
	spangaps = true
	xtype = time 
	ytype = linear
	xlabel = "Time"
	ylabel = "Count"
	ymin = -10
	ymax = 200
	tension = 0.4 # 0 to 1, makes lines smoother
	pointradius = 0</code></pre>
	
	
<h2>Multiple Charts</h2>	
<p>If you have multiple series you would like to show in separate charts, you can use the multichart options to split them up. </p>
<pre><code class="language-cfwquery">| source random records=300 type=series
| chart
	by=[WAREHOUSE, ITEM, CLASS] 
	type=area
	x=TIME 
	y=COUNT 
	multichart=true
	multicharttitle=true
	multichartcolumns=3
	height='150px' # cannot be a percentage value when multichart=true
	showlegend=false</code></pre>


<h2>Display Details</h2>
<p>Use the details parameters to display the chart details with another renderer. </p>
<pre><code class="language-cfwquery">| source random records=300 type=series
| remove TOTAL
| formatfield TIME=['timestamp', "yyyy-MM-dd HH:mm:ss"]
| formatrecord # formats will be applied to details
	[(COUNT >= 90), "cfw-red"]  
	[(COUNT >= 80), "cfw-orange"] 
	[(COUNT >= 70), "cfw-yellow"]  
	[(COUNT >= 60), "cfw-limegreen"]   
	[true, "cfw-green"]  
| chart 
	by=[WAREHOUSE, ITEM, CLASS]
	type=area
	x=TIME 
	y=COUNT 
	multichart=true
	multicharttitle=false
	multichartcolumns=3
	padding='10px 2px'
	height='250px' # cannot be a percentage value when multichart=true
	showlegend=false
	details=true
	detailsrenderer='table'
	detailssize=50 # number representing percentage
	detailsposition=bottom</code></pre>	
	
<h2>Category Bar Chart</h2>
<p>You can create a category bar charts by setting the parameter xtype to category:</p>	
<pre><code class="language-cfwquery">| source random records=100 type=series
| set ITEM = randomFrom(["Apple", "Pear", "Orange", "Peach", "Lemon"])
| stats
	by = [WAREHOUSE, ITEM] 
    COUNT=count()
| chart by = [WAREHOUSE] 
    type=bar
    x=ITEM
    xtype=category
    y=COUNT
    multichart=false
</code></pre>


<h2>Gantt Chart</h2>
<p><b>Note:</b> Using the chart-command for creating Gantt Charts is not recommended if you don't know how much data you have to display. 
Use the <a href="#" onclick="cfw_manual_loadPage('Query|Commands|formatboxplot')">formatboxplot</a> command instead to create a gantt-chart-like view inside of a table.  </p>
<p>Gantt chart has a bit of a special treatment. The by-parameter will be used to create labels, while x/y parameters take start/end date of the bar in the chart.
Following will give you a chart containing a time line for each group created with the by-parameter:</p>	
<pre><code class="language-cfwquery">| source random records=1000 
| filter
	FIRSTNAME ~= 'Hera|Aurora|Luna|Zeus|Heracles|Cupid'
| set
	FROM = LAST_LOGIN
	TO = FROM + (VALUE * 5000000)
| sort FROM
| chart 
	by=[FIRSTNAME] 
	type='gantt'
	x=FROM # start time in epoch milliseconds
	y=TO   # end time in epoch milliseconds
	height="800px"
</code></pre>

<p>To get only one bar per element, you can use the stats command to filter by first and last element:</p>	
<pre><code class="language-cfwquery">| source random records=100
| filter
	FIRSTNAME ~= 'Hera|Aurora|Luna|Zeus|Heracles|Cupid'
| set
	FROM = LAST_LOGIN
	TO = FROM + (VALUE * 1000000)
| sort FROM
| stats
	by=[FIRSTNAME]
	FROM=first(FROM)
	TO=last(TO)
| set 
	FROM = timeformat("yyyy-MM-dd'T'HH:mm:ss", FROM)
	TO = timeformat("yyyy-MM-dd'T'HH:mm:ss", TO)
| chart 
	by=[FIRSTNAME] 
	type='gantt'
	x=FROM # start time in epoch milliseconds
	y=TO   # end time in epoch milliseconds
	</code></pre>

