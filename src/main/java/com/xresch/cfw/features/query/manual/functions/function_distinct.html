
<p>This function is used to get lists of distinct values by their string representation. It supports aggregation and can be used in aggregation commands.
Values types are ignored, a string &quot;42&quot; is considered the same as number 42, same with string &quot;true&quot; and boolean true.</p>

<p>Following query creates random records and uses the distinct-function for aggregation:</p>
<pre><code class="language-cfwquery">| source random records=1000
| keep FIRSTNAME
| set VALUE = if( random(0,20) <19
				, random(0,20)
				, null)
| stats by=[FIRSTNAME] 
	# regular count 
	COUNT=count() 
	MIN=min(VALUE) 
	AVG=avg(VALUE) 
	MAX=max(VALUE)
	DISTINCT=distinct(VALUE) # returns a sorted array of distinct values
	DISTINCT_NULLS=distinct(VALUE, true) # returns a sorted array of distinct values, including null values
	DISTINCT_NOSORT=distinct(VALUE, false, false) # returns an unsorted array of distinct values
| sort FIRSTNAME reverse=true</code></pre>

<p>Here some examples of the distinct-function when used outside of an aggregation command:</p>
<pre><code class="language-cfwquery">| source empty
| set
	DISTINCT_EMPTY		= distinct() 		#returns null
	DISTINCT_NULL		= distinct(null) 	#returns null
	DISTINCT_NUMBER		= distinct(123) 	#returns 123
	DISTINCT_BOOLEAN	= distinct(true) 	#returns true
	DISTINCT_STRING		= distinct("for fluff's sake") # returns "for fluff's sake"
	DISTINCT_ARRAY		= distinct(['a', 'a', 'B', 'B', 'C!', 'C!'], true, true) 		# returns ["a", "B", "C!"]
	DISTINCT_OBJECT		= distinct({a: "one", b: true, c: 3, c: "one", d: true, e: 3}) 	# returns [3, "one", true]
	NO_NULLS			= distinct(['a', 'a', 'b', null, null ], false) 				# returns ["a", "b"]
	NULLS				= distinct(['a', 'a', 'b', null, null ], true) 					# returns ["a", "b", null]
	DO_SORT 			= distinct(['c', 'a', 'b', null ], false) 						# returns ["a", "b", "c"]
	DO_NOT_SORT			= distinct(['c', 'a', 'b', null ], false, false) 				# returns ["c", "a", "b"]
	TYPES				= distinct(['1', 1, '2', 2, 'true', true]) 						# returns ["1", "2", "true"]</code></pre>





