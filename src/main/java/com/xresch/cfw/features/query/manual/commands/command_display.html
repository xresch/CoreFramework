
<p>This command lets you change how the data is rendered. With the 'as' parameter you can choose the renderer that will be used to display the data.</p>

<p>The following query creates 42 random records and displays them as panels. </p>

<pre><code class="language-cfwquery">source random records = 42
| display as=panels</code></pre>

<p>Further you can set the fields that should be visible by providing an array of fieldnames. This might not work for all renderers, for example the JSON display will always show all fields. </p>
<pre><code class="language-cfwquery">| source random
| rename URL = url
| rename LAST_LOGIN = "Last Login"
| display as=table  visiblefields=[FIRSTNAME, LASTNAME, "Last Login", url]</code></pre>


<p>For renderers like panels, cards and tiles, you can set the fields used for the title by setting the parameters titlefields and titleformat.
If titleformat is not set, the field values are joined with a blank as a separator: </p>

<pre><code class="language-cfwquery">| source random
| rename URL = url
| rename LAST_LOGIN = "Last Login"
| display as=panels  
    visiblefields=[FIRSTNAME, LASTNAME, "Last Login", url] 
    titlefields=[INDEX, FIRSTNAME, LASTNAME, LOCATION]
    titleformat="{0}: {2} {1} (Location: {3})"</code></pre>
    
    

<p>The statusmap renderer will only properly display when the menu is turned off using menu=false.
The following example does that: </p>

<pre><code class="language-cfwquery">| source random records=50
| remove _source
| formatrecord 
	[(VALUE >= 80), "cfw-red"] 
	[(VALUE >= 40), "cfw-yellow"] 
	[(VALUE >= 0), "cfw-green"] 
| sort VALUE reverse=true
| display as=statusmap menu=false</code></pre>  
    

<p>To get rid of visible fields, define an empty array(mote: null is default and makes all fields visible).
This is for example useful when displaying as tiles, when you only want to show a single value: </p>

<pre><code class="language-cfwquery">| source random records=100
| remove _source
| formatrecord 
	[(VALUE >= 80), "cfw-red"] 
	[(VALUE >= 40), "cfw-yellow"] 
	[(VALUE >= 0), "cfw-green"] 
| sort VALUE reverse=true
| formatfield 
	VALUE=postfix,"%" 
| display 
	as=tiles 
	menu=false 
	titlefields=[VALUE] 
	visiblefields=[]</code></pre>  
	
	
<p>You can display a button in the menu to give the option to download the data in either CSV, JSON or XML format.
You can also display a store button that allows the user to store their data in the File Manager: </p>

<pre><code class="language-cfwquery">| source random
| display as=table
	download = true
	store = true
	# menu=button # above buttons do not work when the menu is displayed as a button</code></pre>  
	

