
<p><b>Video Tutorial:&nbsp;</b> <a target="newtab" href="https://www.youtube.com/watch?v=kX9FGuc7mWY"><i class="fab fa-youtube"></i>&nbsp;Youtube</a> </p>

<p>Used to create statistical matrix using the values of one field as columns and one or more fields for grouping the rows.
</p>

<p>The following statsmatrix example uses the values of CLASS for the columns and 3 other fields for grouping the rows:
</p>
<pre><code class="language-cfwquery">| meta BY = [ITEM, BOX_SIZE, CLASS]
| source random records=100 type=series
| sort CLASS # sort by field used as statsmatrix column
| statsmatrix 
	column=CLASS
	row=meta(BY)
	records=count() # number of records aggregated
	COUNT=sum(COUNT) # total count of items
	PRICE_AVG=avg( round(PRICE, 2) ) 
	TOTAL_VALUE=sum( round(PRICE*COUNT, 2) ) 
| sort meta(BY)
| off formatfield # this is not needed anymore, done by statsmatrix out-of-the-box
	fields(meta(BY))=['list', "none"] # for all fields except BY-fields
	meta(BY)=[
		 ['align', "center"]
		,['css','color', "white"]
	]</code></pre>



<h2>Compacting with Crates-Command</h2>
<p>The crates command can be very useful to make the results of statsmatrix more compact.
Following creates a matrix based on alphabet and value ranges:
</p>
<pre><code class="language-cfwquery">| source random records=1000
| crates by=FIRSTNAME 
		 type=alpha
		 step=3
		 name=ALPHA
| crates by=VALUE
		 type=number
		 step=5
		 maxgroups=10
| sort ALPHA
| statsmatrix
		 column=ALPHA
		 row=CRATE
		 MIN=min(VALUE)
		 AVG=avg(VALUE)
		 SUM=sum(VALUE)</code></pre>	

<h2>Combining with displayfields</h2>
<p>For a more visual display of data you can aggregate the records using records()-function for aggregation and displayfields
</p>
<pre><code class="language-cfwquery">| meta BY = [ITEM]
| source random records=100 type=series
| formatrecord
	[(COUNT == null), "cfw-gray"] 
	[(COUNT &lt; 20), "cfw-red"] 
	[(COUNT &lt; 40), "cfw-orange"]  
	[(COUNT &lt; 60), "cfw-yellow"] 
	[(COUNT &lt; 80), "cfw-limegreen"] 
	[true, "cfw-green"] 
| statsmatrix 
	column=CLASS
	row=meta(BY)
	records=records() # makes an array of the whole records
| displayfields
	fields=fields(meta(BY)) # All fields except BY fields
	as=tileandbar
	visiblefields=[FIRSTNAME,LASTNAME]
	height="100px"
	#width="100px"</code></pre>	
	

	
<h2>Making Tables from Flat Record/Fieldname/Value Structures</h2>
<p>This command can help you creating actual tables from flat data structures.
You get such structures for example when you have multiple lines per record, each containing a record ID, the fieldname and the value assigned to the fieldname.
Here an example of such data:</p>
<pre class="card monospace p-3 mb-3">key				|	fieldname	|	text

data.record[0]	|	ID			|	c8e454aa-2cf2-40a4-bb3
data.record[0]	|	FIRSTNAME	|	Hermes
data.record[0]	|	LASTNAME	|	Viklund
data.record[0]	|	LOCATION	|	Quivira

data.record[1]	|	ID			|	2d0c6180-3852-4e8b-961
data.record[1]	|	FIRSTNAME	|	Artemis
data.record[1]	|	LASTNAME	|	Fransson
data.record[1]	|	LOCATION	|	Zerzura	

data.record[2]	|	ID			|	6a07f433-d835-4de4-88c
data.record[2]	|	FIRSTNAME	|	Nike
data.record[2]	|	LASTNAME	|	Lindberg
data.record[2]	|	LOCATION	|	Finias</pre>

<p>Following example extracts records from an XML-Export and uses the unbox &amp; statsmatrix command to turn it into a regular table:</p>
<pre><code class="language-cfwquery">| source file 
	as = 'xmltree'
	file = {"id": 134, "name": "data.xml"}
	/* Above data contained records like the following at some point in human history:
	&lt;data>
		&lt;record>
	   		&lt;firstname>Aurora&lt;/firstname>
	   		&lt;lastname>Testonia&lt;/lastname>
	   		&lt;country>Ireland&lt;/country>
	   		&lt;dessert withcream="true" creamWipped="yes please">Tiramisu&lt;/dessert>
		&lt;/record>
		  ... many more records ... 
	&lt;/data> 
	*/
| unbox replace = true children
| unbox replace = true children
| unbox replace = false children
| unbox replace = true 
		key # keep key
		children.tag
		children.text
		children.attributes
| statsmatrix 
	column = 'tag'
	row = 'key'
	value = first(text) # as there is anyway only one value
| sort key
| remove key</code></pre>