
<p><b>Video Tutorial:&nbsp;</b> <a target="newtab" href="https://www.youtube.com/watch?v=xiC3hZPWnuc"><i class="fab fa-youtube"></i>&nbsp;Youtube</a> </p>

<p>Formats the record with colors based on the conditions you specify.
The conditions are specified as arrays. The first element of the array is the condition to evaluate. 
The seconds is the background color and the third is the text color.
All conditions you specify are evaluated in order like. The first condition that matches is applied and following are ignored. 
If you want to specify a default case, use 'true' as the condition. 
</p>

<p>The following example colors the records based on the VALUE field with the cfw threshhold colors. Also it removes the default format from LIKES_TIRAMISU.
</p>
<pre><code class="language-cfwquery">| source random records=1000
| remove _source
| formatrecord 
	[(VALUE >= 80), "cfw-red"] 
	[(VALUE >= 60), "cfw-orange"]  
	[(VALUE >= 40), "cfw-yellow", "purple"] 
	[(VALUE >= 20), "cfw-limegreen"] 
	[(VALUE >= 10), "cfw-green"] 
	[true, "cfw-gray"] 
| formatfield LIKES_TIRAMISU=none</code></pre>


<p>You are free to use any binary expression you like for the condition. For the colors you can use either a 'cfw-' color class, a css color or a hex-color. 
</p>
<pre><code class="language-cfwquery">| source random records=1000
| remove _source
| formatrecord 
	[(FIRSTNAME ~='^He'), "#332288"] 
	[(VALUE >= 80 OR LIKES_TIRAMISU==false), "red"] 
	[(LIKES_TIRAMISU==true), "green"] 
	[true, "cfw-gray"]
| formatfield LIKES_TIRAMISU=none</code></pre>

<p>Using this command and combining it with the other format commands formatfield and display, you can create a traffic light overview for your records like this: 
</p>
<pre><code class="language-cfwquery">| source random records=100
| remove _source
| formatrecord 
	[(VALUE >= 80), "cfw-red"] 
	[(VALUE >= 40), "cfw-yellow"] 
	[(VALUE >= 0), "cfw-green"] 
| sort VALUE reverse=true
| formatfield 
	VALUE=postfix,"%" 
	VALUE=css,"font-size","24px"
| display as=tiles menu=false titlefields=[VALUE] visiblefields=[]</code></pre>





