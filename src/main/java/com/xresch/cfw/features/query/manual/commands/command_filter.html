
<p>Filters fields based on the conditions you specify.</p>

<p>Following code will only keep records where the firstname is equals Athena: </p>
<pre><code class="language-cfwquery">| source random
| filter FIRSTNAME==Athena</code></pre>


<p>This example keeps all records with firstname Venus and a lastname that matches the regex expression (ending with "son"). 
If you do not specify any of the keywords AND/OR, and is used implicitely to combine multiple conditions. </p>
<pre><code class="language-cfwquery">| source random records=10000
| filter FIRSTNAME==Venus  LASTNAME~=".*son$"</code></pre>


<p>The following code shows an example of how to use the keywords AND, OR and NOT, as well as how to group conditions using braces.</p>
<pre><code class="language-cfwquery">| source random records=10000
    #only humans called 'Luna' or with a lastname that does not end with "son"
| filter (FIRSTNAME==Luna OR NOT LASTNAME~=".*son$") 
	# only people that like tiramisu
	NOT (LIKES_TIRAMISU==false)
	# only record where value is bigger than index
	NOT (VALUE / INDEX ) &lt;= 1</code></pre>


	
<p>You can as well use arithmetic expressions in the filter query.</p>

<pre><code class="language-cfwquery">| source random type=numbers records=100000 limit=100000
| filter 
	_source~="^ra.*"+"dom" 
	AND (THOUSANDS >= 1000^3 AND FLOAT >= (1000^2)*9 ) 
	OR (BIG_DECIMAL>=700000 AND BIG_DECIMAL&lt;800000 ) 
| formatfield 
	[THOUSANDS,FLOAT,BIG_DECIMAL]=separators
	[THOUSANDS,FLOAT,BIG_DECIMAL]=['threshold', 0, 1000, 1000^2, 1000^3, (1000^4)*500, "text"]</code></pre>


	
<p>If you process fields that contain null values, here is how the query language handles null values in binary expressions.
The following filter will evaluate always to true as all conditions are true.</p>

<pre><code class="language-cfwquery">| source random type=numbers records=10000 limit=10000
| filter 
	#in equal and not equal comparison, null is handled as null and only equal to itself
	null == null
	AND (null != null) == false 
	AND (null != "anythingElse") == true
	AND (null != false) == true
	AND (null != 123) == true
	# Division by null or zero will evaluate to null
	AND (100 / 0) 	== null
	AND (1 / null) 	== null
	AND (0 / 0) 	== null
	# in arithmetic expressions, null will be converted to zero
	AND (null / 1) 	== 0
	AND (0 / 100) 	== 0
	AND (null * 10)	== 0
	AND (10 * null)	== 0
	AND (null - 10)	== -10
	AND (10 - null)	== 10	
	AND (null + 10)	== 10
	AND (10 + null)	== 10
	# for lower/greater than comparison, null will be converted to zero
	AND (null &lt; 1)	== true
	AND (null &lt;= 1)	== true
	AND (1 > null)	== true
	AND (1 >= null)	== true
	# results of regex operator is always false if either side is null 
	AND ("myString" ~= null) == false
	AND (null ~= ".*") == false
	AND (null ~= null) == false
	# results of AND/OR with either side null 
	AND (null AND true) == false
	AND (null OR true) == true 
</code></pre>


