
<p><b>Video Tutorial:&nbsp;</b> <a target="newtab" href="https://www.youtube.com/watch?v=ypQtTs9AbK0"><i class="fab fa-youtube"></i>&nbsp;Youtube</a> </p>

<p>This command is used to mimic the behavior of a previous query.
It is useful to copy a query that you want to execute with different parameters(specified with command 'metadata' or 'globals') without having to write the 
whole query twice. 
The mimicked query will be inserted at the place where mimic is used.
Mimic commands cannot be nested. Any mimic command in a mimicked query will always be removed.
</p>

<p>Following example executes 2 queries, and the third one mimics the previous one:
</p>
<pre><code class="language-cfwquery">| globals multidisplay=3 # display 3 results in one row
| source random records = 5
| keep TIME, LOCATION
; # End of First Query
| source random records = 10
| keep TIME, VALUE
; # End of Second Query
| mimic # copies and executes the query right before this one</code></pre>



<p>You can use the parameter 'name' to select the query to mimic.
Use the command 'metadata' to set the name. By default, 'metadata' commands are removed when mimicked:
</p>
<pre><code class="language-cfwquery">| globals multidisplay=3 # display 3 results in one row
| metadata name="mimicThis"
| source random records = 5
| keep TIME, LOCATION
; # End of First Query
| source random records = 10
| keep TIME, VALUE
; # End of Second Query
| mimic name="mimicThis" # copies and executes the first query named 'mimicThis'</code></pre>



<p> Using the metadata command, you can specify that a query is a template and not be executed.
This can be useful to reuse a query, like in the following example, where formatting is specified and reused in multiple queries:
</p>
<pre><code class="language-cfwquery">| metadata
	name="format"  
	template=true # mark this query as a template
| formatfield
	VALUE = ['threshold', 44, 55, 66, 77, 88, "bg"]
| formatrecord
	[(VALUE == null), "cfw-gray"] 
	[(VALUE &lt; 50), "cfw-green"] 
	[true, "cfw-red"] 
| display as=tiles menu=false
; 

#############################################
| record
	[NAME, VALUE]
	["Raurova", 44]
	["Rakesh", 77]
| mimic name="format" # insert formatter
; 

#############################################
| record
	[NAME, VALUE]
	["Vera", 66]
	["Aloe", 22]
| mimic name="format" # insert formatter again

</code></pre>



<p>You can select the commands which should be removed using the parameter 'remove'.
This allows you to remove commands at wish. Notice that this will override the default behavior of 
removing 'metadata' commands, so you have to include it manually if you want to keep this behavior.
Additionally you can override metadata and therefore parameterize your query.
</p>
<pre><code class="language-cfwquery">| globals multidisplay=3 # display 3 results in one row
| metadata 
	name="test" # set name of this query
	custom=["A","B","C"] # a custom parameter
| source random records = 15
| set CUSTOM = randomFrom(meta(custom)) # use custom parameter
| keep TIME, LOCATION, CUSTOM
; # End of First Query
| source random records = 10
| keep TIME, VALUE
; # End of Second Query
| metadata 
	custom=["X","Y","Z"] # set metadata for this query
| mimic name="test" remove=[metadata, keep]  # select query "test" and remove commands "metadata" and "keep"
| keep CUSTOM, VALUE # define keep for this query</code></pre>

