
<p>
	The query language supports the creation and use of objects and arrays (further in this text 'objects' will include arrays too).
	This can be helpful in use cases like  fetching data from sources returning JSON structures, 
	or when using the API to execute queries and structuring the returned result.
</p>

<h2>Formatting Objects</h2>
<p>For easier reading of objects, you can use the formatfield-command to format an object as a list:
</p>
<pre><code class="language-cfwquery">| source random records=10 type=various
| keep GRAPH_DATA
| formatfield 
	GRAPH_DATA = ['list', "bullets", "10px", false]</code></pre>
	
<h2>Check Type and Parsing JSON</h2>
<p>Objects are printed as strings by default, what makes it hard to see if they are of type string or are actual objects.
You can use the type()-function to print the data type, and use the tojson()-function to convert to an object.
If you pass an object to the function it will return the object unchanged, what makes it useful if you don't know if it is still a string or already an object.
</p>
<pre><code class="language-cfwquery">| source empty
| set 
	MESSAGE = '{index: 1, message:"Hello World"}'
	TYPE = type(MESSAGE)   # returns 'string'
	
	# ================================
	OBJECT=tojson(MESSAGE) 
	OBJECT_TYPE = TYPE = type(OBJECT) # returns 'object'
	
	# ================================
	ARRAY=tojson('[1,2,3, "a", "b", "c", true, false]')
	ARRAY_TYPE = TYPE = type(ARRAY) # returns 'array'</code></pre>

<h2>Unboxing Objects</h2>
<p>To convert all the members of an object into fields, or entries of an array to rows, use the <a href="#" onclick="cfw_manual_loadPage('Query|Commands|unbox');">unbox</a> command:</p>
<pre><code class="language-cfwquery">| source empty
| set
  object = {index: 1, message:"Hello World"}
  array = [null, one, true, "three"]
| unbox replace=false
	object # converts fields to columns
	array # converts each entry to a new row</code></pre>

	
<h2>Creating Objects</h2>
<p>You can create your own objects for every record using curly braces'{}'.
Here an example on how to do that:
</p>
<pre><code class="language-cfwquery">| source empty records=10
| set
  #########################################
  # Static Objects
  #########################################
  # Following will parse everything inside {...} as string and converts it to an object.
  # functions and fields will not be resolved, e.g. 'index' will be string "INDEX", not the value of the field before 
  staticobject = {id: 0, name: "Tereza", makes_tiramisu: true, index: INDEX}
  #########################################
  # Dynamic Objects
  #########################################
  # create an empty object, afterwards you can add data using the dot-operator
  object = {}
  object.index = INDEX # will take value from index
  object.url = "https://www.clickme4tiramisu.com/id="+ID+"&amp;firstname="+encode(FIRSTNAME)
  object.percent = random()
  object.float = randomFloat()
  object.category = randomFrom(['X', 'Y', 'Z'])
  object.isIsness = randomFrom([true, false, null])
  object.subobject.value = randomFrom([111, 222, 333])
  #########################################
  # Object Function
  #########################################
  functionObject = 
  		object(
  			"id", 22
  			"name", "Roberta"
  			"likesPannaCotta", true
  			"status", null
  			"tags", ["beauty", "italian", "model", "dancer"]
  			"attributes", object("height", 172, "eyeColor", "green" )
  		)
| formatfield 
	fields() = ['list', "bullets", "10px", false]</code></pre>


<h2>Accessing Object Members</h2>
<p>To access or overwrite the value of an object member, you can use the dot-operator:</p>
<pre><code class="language-cfwquery">| source random type=various records=10
| keep GRAPH_DATA
| set
	X=GRAPH_DATA.x # assign value of member "GRAPH_DATA.x" to "X"
	Y=GRAPH_DATA.y # assign value of member "GRAPH_DATA.y" to "Y"
	MULTIPLIED = (GRAPH_DATA.x * GRAPH_DATA.y) # use in math
	GRAPH_DATA_REF = GRAPH_DATA # creates a reference to the object(affected by changes to original)
	GRAPH_DATA_CLONE = clone(GRAPH_DATA) # create a clone of the object(not affected by changes to original)
	GRAPH_DATA.rounded.x = round(GRAPH_DATA.x, 2) # physically forms the number so it rolls down a hill
	GRAPH_DATA.rounded.y = round(GRAPH_DATA.y, 2) # might make a circle around the number, or just makes a circle and ignores the number
| filter 
	(GRAPH_DATA.rounded.x &lt;= 5) # use with operators
| formatfield
	[Y, MULTIPLIED] = ['decimals', 2] # just some formatting
	# [GRAPH_DATA.y] = ['decimals', 2] # this does not work</code></pre>



<p>In case you want to read the member name of the object from a record field, use '.[fieldname]' to access the member.
Following query gets a random member name and accesses its value with the '.[randomName]' operator:</p>
<pre><code class="language-cfwquery">| source empty records= 100
| set
  index = count()
  #########################################
  # Prepare Object
  #########################################
  object = {}
  object.percent = random()
  object.float = randomFloat()
  object.category = randomFrom(['X', 'Y', 'Z'])
  object.subobject.value = randomFrom([111, 222, 333])
  #########################################
  # Random from Object
  #########################################
  randomName = randomFrom(object) # get a random member name from the object (either: percent | float | category | subobject)
  memberValue = object.[randomName] # get the value of the random member
  memberSubValue = object.[randomName].value # access a value of a sub object</code></pre>
  
<p>When you use functions and the return value is an object or array, you can access members directly.
Also you can use function return values inside of access expressions:</p>
<pre><code class="language-cfwquery">| paramdefaults
	object = {"test": "hello"}
	array = ["hello", "World"]
	favoriteField = "favorite"
	objectField = "object"
| source empty
| set
	###################################################
  	# Access member from function return value
  	###################################################
	OBJECT = param(object).test 				# returns "hello"
	OBJECT_B = param(object)[test]				# returns "hello"
	OBJECT_C = param(object).[test]				# returns "hello"
	ARRAY = param(array)[1] 					# returns "World"
	ARRAY_B = param(array).[1] 					# returns "World"
	###################################################
  	# Use function return value in access expression
 	###################################################
	temp = {"favorite": "tiramisu", "object": { "sub": 42  } }
	FUNC_ACCESS = temp.param(favoriteField)			# returns "tiramisu"
	FUNC_SUBACCESS = temp.param(objectField).sub	# returns 42	</code></pre>




